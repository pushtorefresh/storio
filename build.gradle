// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        // Android Gradle plugin
        classpath 'com.android.tools.build:gradle:1.2.1'

        // Resolves android-sdk dependencies
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'

        // APT compile-time annotation processing
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

ext {
    compileSdkVersion = 22
    buildToolsVersion = '22.0.0'

    supportAnnotations = 'com.android.support:support-annotations:21.0.3'
    rxJava = 'io.reactivex:rxjava:1.0.9'

    dagger = 'com.google.dagger:dagger:2.0'
    daggerCompiler = 'com.google.dagger:dagger-compiler:2.0'

    javaxInject = 'javax.inject:javax.inject:1'
    javaxAnnotationApi = 'javax.annotation:javax.annotation-api:1.2'

    appCompat = 'com.android.support:appcompat-v7:22.0.0'
    butterKnife = 'com.jakewharton:butterknife:6.1.0'
    recyclerView = 'com.android.support:recyclerview-v7:22.0.0'
    rxAndroid = 'io.reactivex:rxandroid:0.24.0'
    timber = 'com.jakewharton.timber:timber:2.7.1'

    autoService = 'com.google.auto.service:auto-service:1.0-rc2'
    javaPoet = 'com.squareup:javapoet:1.0.0'
    intellijAnnotations = 'com.intellij:annotations:12.0'

    junit = 'junit:junit:4.12'
    mockitoCore = 'org.mockito:mockito-core:1.10.19'
    testingSupportLib = 'com.android.support.test:testing-support-lib:0.1'
}

// Option to disable Pre-Dexing on CI env
project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)
                || "com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {

            // enable or disable pre-dexing
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs

            // treat all Android Lint warning in all sub-projects as errors
            project.android.lintOptions.warningsAsErrors = true

            // StorIO should support Java 6
            project.android.compileOptions.sourceCompatibility = JavaVersion.VERSION_1_6
            project.android.compileOptions.targetCompatibility = JavaVersion.VERSION_1_6
        }
    }

    project.afterEvaluate {
        // StorIO Sample App uses Dagger's annotations and Java Compiler throws warning:
        // 'warning: No processor claimed any of these annotations suppress'
        // Unfortunately this warning can not be ignored
        // Please feel free to suggest workaround for this problem
        if (!'storio-sample-app'.equals(project.name)
                && !'storio-sqlite-annotation'.equals(project.name)
                && !'storio-sqlite-annotation-processor'.equals(project.name)) {
            tasks.withType(JavaCompile) {
                // treat all Java Compiler warnings as errors
                options.compilerArgs << "-Xlint:all" << "-Werror"
            }
        }
    }
}
